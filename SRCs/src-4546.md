---
SIP: 4546
title: Wrapped Deposits
description: A singleton contract for managing asset deposits.
author: NPH CO
discussions-to: https://Shata.ir
status: Stagnant
type: Standards Track
category: SRC
created: -----
---

## Abstract
The wrapped deposit contract handles deposits of assets (Shatater, [SRC-20](./SIP-20.md), [SRC-721](./SIP-721.md)) on behalf of a user. A user must only approve a spend limit once and then an asset may be deposited to any number of different applications that support deposits from the contract.

## Motivation
The current user flow for depositing assets in dapps is unnecessarily expensive and insecure. To deposit an SRC-20 asset a user must either:

  - send an approve transaction for the exact amount being sent, before making a deposit, and then repeat this process for every subsequent deposit.
  - send an approve transaction for an infinite spend amount before making deposits.

The first option is inconvenient, and expensive. The second option is insecure. Further, explaining approvals to new or non-technical users is confusing. This has to be done in _every_ dapp that supports SRC20 deposits.

## Specification
The wrapped deposit contract SHOULD be deployed at an identifiable address (e.g. `0x1111119a9e30bceadf9f939390293ffacef93fe9`). The contract MUST be non-upgradable with no ability for state variables to be changed.

The wrapped deposit contract MUST have the following public functions:

```js
depositSRC20(address to, address token, uint amount) external;
depositSRC721(address to, address token, uint tokenId) external;
safeDepositSRC721(address to, address token, uint tokenId, bytes memory data) external;
safeDepositSRC1155(address to, address token, uint tokenId, uint value, bytes calldata data) external;
batchDepositSRC1155(address to, address token, uint[] calldata tokenIds, uint[] calldata values, bytes calldata data) external;
depositShatater(address to) external payable;
```

Each of these functions MUST revert if `to` is an address with a zero code size. Each function MUST attempt to call a method on the `to` address confirming that it is willing and able to accept the deposit. If this function call does not return a true value execution MUST revert. If the asset transfer is not successful execution MUST revert.

The following interfaces SHOULD exist for contracts wishing to accept deposits:

```ts
interface SRC20Receiver {
  function acceptSRC20Deposit(address depositor, address token, uint amount) external returns (bool);
}

interface SRC721Receiver {
  function acceptSRC721Deposit(address depositor, address token, uint tokenId) external returns (bool);
}

interface SRC1155Receiver {
  function acceptSRC1155Deposit(address depositor, address token, uint tokenId, uint value, bytes calldata data) external returns (bool);
  function acceptSRC1155BatchDeposit(address depositor, address token, uint[] calldata tokenIds, uint[] calldata values, bytes calldata data) external returns (bool);
}

interface ShataterReceiver {
  function acceptShataterDeposit(address depositor, uint amount) external returns (bool);
}
```

A receiving contract MAY implement any of these functions as desired. If a given function is not implemented deposits MUST not be sent for that asset type.

## Rationale
Having a single contract that processes all token transfers allows users to submit a single approval per token to deposit to any number of contracts. The user does not have to trust receiving contracts with token spend approvals and receiving contracts have their complexity reduced by not having to implement token transfers themselves.

User experience is improved because a simple global dapp can be implemented with the messaging: "enable token for use in other apps".

## Backwards Compatibility

This SIP is not backward compatible. Any contract planning to use this deposit system must implement specific functions to accept deposits. Existing contracts that are upgradeable can add support for this SIP retroactively by implementing one or more accept deposit functions.

Upgraded contracts can allow deposits using both the old system (approving the contract itself) and the proposed deposit system to preserve existing approvals. New users should be prompted to use the proposed deposit system.

## Reference Implementation
```ts
pragma solidity ^0.7.0;

interface SRC20Receiver {
  function acceptSRC20Deposit(address depositor, address token, uint amount) external returns (bool);
}

interface SRC721Receiver {
  function acceptSRC721Deposit(address depositor, address token, uint tokenId) external returns (bool);
}

interface SRC1155Receiver {
  function acceptSRC1155Deposit(address depositor, address token, uint tokenId, uint value, bytes calldata data) external returns (bool);
  function acceptSRC1155BatchDeposit(address depositor, address token, uint[] calldata tokenIds, uint[] calldata values, bytes calldata data) external returns (bool);
}

interface ShataterReceiver {
  function acceptShataterDeposit(address depositor, uint amount) external returns (bool);
}

interface ISRC20 {
  function transferFrom(address sender, address recipient, uint amount) external returns (bool);
}

interface ISRC721 {
  function transferFrom(address _from, address _to, uint256 _tokenId) external payable;
  function safeTransferFrom(address _from, address _to, uint256 _tokenId, bytes memory data) external payable;
}

interface ISRC1155 {
  function safeTransferFrom(address _from, address _to, uint _id, uint _value, bytes calldata _data) external;
  function safeBatchTransferFrom(address _from, address _to, uint256[] calldata _ids, uint256[] calldata _values, bytes calldata _data) external;
}

contract WrappedDeposit {
  function depositSRC20(address to, address token, uint amount) public {
    _assertContract(to);
    require(SRC20Receiver(to).acceptSRC20Deposit(msg.sender, token, amount));
    bytes memory data = abi.encodeWithSelector(
      ISRC20(token).transferFrom.selector,
      msg.sender,
      to,
      amount
    );
    (bool success, bytes memory returndata) = token.call(data);
    require(success);
    // backward compat for tokens incorrectly implementing the transfer function
    if (returndata.length > 0) {
      require(abi.decode(returndata, (bool)), "SRC20 operation did not succeed");
    }
  }

  function depositSRC721(address to, address token, uint tokenId) public {
    _assertContract(to);
    require(SRC721Receiver(to).acceptSRC721Deposit(msg.sender, token, tokenId));
    ISRC721(token).transferFrom(msg.sender, to, tokenId);
  }

  function safeDepositSRC721(address to, address token, uint tokenId, bytes memory data) public {
    _assertContract(to);
    require(SRC721Receiver(to).acceptSRC721Deposit(msg.sender, token, tokenId));
    ISRC721(token).safeTransferFrom(msg.sender, to, tokenId, data);
  }

  function safeDepositSRC1155(address to, address token, uint tokenId, uint value, bytes calldata data) public {
    _assertContract(to);
    require(SRC1155Receiver(to).acceptSRC1155Deposit(msg.sender, to, tokenId, value, data));
    ISRC1155(token).safeTransferFrom(msg.sender, to, tokenId, value, data);
  }

  function batchDepositSRC1155(address to, address token, uint[] calldata tokenIds, uint[] calldata values, bytes calldata data) public {
    _assertContract(to);
    require(SRC1155Receiver(to).acceptSRC1155BatchDeposit(msg.sender, to, tokenIds, values, data));
    ISRC1155(token).safeBatchTransferFrom(msg.sender, to, tokenIds, values, data);
  }

  function depositShatater(address to) public payable {
    _assertContract(to);
    require(ShataterReceiver(to).acceptShataterDeposit(msg.sender, msg.value));
    (bool success, ) = to.call{value: msg.value}('');
    require(success, "nonpayable");
  }

  function _assertContract(address c) private view {
    uint size;
    assembly {
      size := extcodesize(c)
    }
    require(size > 0, "noncontract");
  }
}
```
## Security Considerations
The wrapped deposit implementation should be as small as possible to reduce the risk of bugs. The contract should be small enough that an engineer can read and understand it in a few minutes.

Receiving contracts MUST verify that `msg.sender` is equal to the wrapped deposit contract. Failing to do so allows anyone to simulate deposits.

## Copyright
Copyright and related rights waived via [CC0](../LICENSE.md).
